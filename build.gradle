defaultTasks('hi') //gets executed when running 'gradlew' without any argument
allprojects {
    task hi {
        group = "fun-tasks"
        description = "Shows greeting from the project :)"
        doLast { task -> println "Hi from $task.project.name" }
    }
}
subprojects {
    apply plugin: 'java'
    group = 'pl.jlabs'
    version = '1.0'
    sourceCompatibility = 1.13 //System.getProperty("java.version") or "13" or JavaVersion.VERSION_13
    repositories {
        jcenter()
        mavenCentral()
        maven {
            credentials {
                username = "${repoUser}"
                password = "${repoPass}"
            }
            url "http://my.private.artifactory/url/goes/here"
        }
    }
    apply from: "${rootProject.projectDir}/commonDependencies.gradle"
}
subprojects { subproject ->
    dependencies {
        //this is for avoiding the circular dependencies
        if (subproject.name != 'data-model' && subproject.name != 'utils') {
            implementation project(":shared:data-model")
            implementation project(":shared:utils")
        }
    }
}
